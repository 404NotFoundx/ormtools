/* ************************************************************************
 *
 * Copyright (C) 2020 tiansheng All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * ************************************************************************/

/*
 * Creates on 2020/7/18.
 */

import org.jiakesimk.minipika.gradle.maven.MavenBuilder

plugins {
  id 'java'
  id 'groovy'
}

apply plugin: "java"
apply plugin: 'maven'
apply plugin: "groovy"
apply plugin: "maven-publish"

ext {
  slf4jVersion = "1.7.21"
  mysqlConnectVersion = "8.0.20"
  commonsCodecVersion = "1.11"
  lombokVersion = "1.18.10"
  fastjsonVersion = "1.2.62"
  jsqlparseVersion = "3.1"
  jom2Version = "2.0.6"
  javassistVersion = "3.12.1.GA"
  logbackVersion = "1.2.3"
  junitVersion = "4.13-rc-2"
  asmToolVersion = "0.0.2"
  aspectJVersion = "1.8.10"
  groovyAllVersion = "3.0.4"
  kotlinVersion = "1.3.72"
}

sourceSets {
  main {
    java { srcDirs = [] }
    groovy {
      // noinspection GroovyAssignabilityCheck
      srcDirs = ["src/main/java", "src/main/groovy"]
    }
  }
  test {
    java { srcDirs = [] }
    groovy {
      // noinspection GroovyAssignabilityCheck
      srcDirs = ["src/test/java"]
    }
  }
}

allprojects {
  repositories {
    mavenLocal()
    maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
  }
}

dependencies {
  implementation "org.jdom:jdom2:$jom2Version"
  implementation "org.slf4j:slf4j-api:$slf4jVersion"
  implementation "org.projectlombok:lombok:$lombokVersion"
  implementation "com.alibaba:fastjson:$fastjsonVersion"
  implementation "commons-codec:commons-codec:$commonsCodecVersion"
  implementation "com.github.jsqlparser:jsqlparser:$jsqlparseVersion"
  implementation "com.github.houbb:asm-tool:$asmToolVersion"
  implementation "commons-codec:commons-codec:${commonsCodecVersion}"
  implementation "javassist:javassist:${javassistVersion}"
  implementation "org.codehaus.groovy:groovy-all:$groovyAllVersion"
  implementation fileTree(dir: "lib", includes: ["*jar"])
  testImplementation "junit:junit:$junitVersion"
  testImplementation "mysql:mysql-connector-java:$mysqlConnectVersion"
}

group = "org.jiakesimk"
version = "v1"
sourceCompatibility = "11"

tasks.withType(GroovyCompile) {
  groovyOptions.setEncoding "UTF-8"
  groovyOptions.setParameters true
}

tasks.withType(JavaCompile) {
  options.encoding = "UTF-8"
  options.compilerArgs << "-parameters"
}

task buildMaven {
  pom {
    project {
      groupId getGroupId()
      artifactId getArtifactId()
      version getGroupId()
    }
  }.withXml {
    def implementations = configurations.implementation.allDependencies
    def asNode = (asNode() as Node)
    // 删除原有的dependencies节点
    MavenBuilder.removeNode(asNode, 'dependencies')
    MavenBuilder.addDependencies(asNode, implementations)
    // 添加maven-compiler-plugin插件
    MavenBuilder.addPluginNode(asNode) {
      it.appendNode('groupId', 'org.apache.maven.plugins')
      it.appendNode('artifactId', 'maven-compiler-plugin')
      def configuration = it.appendNode('configuration')
      configuration.appendNode('compilerArgs', '-parameters')
    }
    // 添加gmavenplus插件
    MavenBuilder.addPluginNode(asNode) {
      it.appendNode('groupId', 'org.codehaus.gmavenplus')
      it.appendNode('artifactId', 'gmavenplus-plugin')
      it.appendNode('version', '1.9.0')
      def goals = it.appendNode('executions').appendNode('execution').appendNode('goals')
      goals.appendNode('goal', 'addSources')
      goals.appendNode('goal', 'addTestSources')
      goals.appendNode('goal', 'generateStubs')
      goals.appendNode('goal', 'compile')
      goals.appendNode('goal', 'generateTestStubs')
      // 设置编译时保留参数名
      def configuration = it.appendNode('configuration')
      configuration.appendNode('parameters', 'true')
      def source = configuration.appendNode('sources').appendNode('source')
      source.appendNode('directory', '${project.basedir}/src/main/')
      source.appendNode('includes').appendNode('include', '**/*.groovy')
    }
  }.writeTo("pom.xml")
}

